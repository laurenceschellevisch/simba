(*

SRL Debug Log
=============

Routines that write to the SRL log file.

.. contents::

*)

var
  SRL_SavePath: string;        // the path to save log files
  SRL_StartTime: string;       // the time the script starts (used to name log file)
  SRL_DisableLogging: boolean; // gives the scripter the option to disable

(*
TakeScreen
~~~~~~~~~~

.. code-block:: pascal

    procedure TakeScreen(Reason: string);

Sets up Box variables

.. note::

    by Mutant, Squirrle, Yakman, & Harry
    Last Modified: Feb. 16th, 2012 by Coh3n

Example:

.. code-block:: pascal

  TakeScreen('Prison Pete');

*)
procedure TakeScreen(Reason: string);
begin
  if (not DirectoryExists(SRL_SavePath)) then
    ForceDirectories(SRL_SavePath);

  try
    if FileExists(SRL_SavePath + reason + ' ' + Players[CurrentPlayer].Name + '.png') then
      SaveScreenshot(SRL_SavePath + reason + ' ' + Players[CurrentPlayer].Name + ' ' + TimeRunning + '.png')
    else
      SaveScreenshot(SRL_SavePath + reason + ' ' + Players[CurrentPlayer].Name + '.png');

    WriteLn('Screenshot of: ' + reason + ' ' + TimeRunning);
  except
    Writeln('Failed to save screenshot of: ' + reason);
  end
end;

(*
OpenSRLLogFile
~~~~~~~~~~~~~~

.. code-block:: pascal

    function OpenSRLLogFile(): integer;

Creates the SRL log file.

.. note::

    by Coh3n
    Last Modified: Feb. 20th, 2012 by Coh3n

Example:

.. code-block:: pascal

  OpenSRLLogFile();

*)
function OpenSRLLogFile(): integer;
var
  fileName: string;
begin
  if (SRL_DisableLogging) then
    exit;

  if (not directoryExists(SRL_SavePath)) then
    forceDirectories(SRL_SavePath);

  // make sure it's a valid file name (Windows)
  fileName := 'SRL log ('+SRL_StartTime+').txt';
  fileName := replace(fileName, '/', '-');
  fileName := replace(fileName, ':', ' ');
  fileName := SRL_SavePath + fileName;

  try
    if (fileExists(fileName)) then
      result := appendFile(fileName)
    else
      result := createFile(fileName);
  except
    writeln('OpenSRLLogFile: '+exceptionToString(exceptionType, exceptionParam));
  end;
end;

(*
AddToSRLLog
~~~~~~~~~~~

.. code-block:: pascal

    procedure AddToSRLLog(s : string);

Writes the string 's' to the SRL log file.

.. note::

    by Markus
    Last Modified: Feb. 20th, 2012 by Coh3n

Example:

.. code-block:: pascal

  AddToSRLLoc('Adding to SRL log file!');

*)
procedure AddToSRLLog(s: string);
var
  tmpFile: integer;
begin
  writeln(s);

  if (SRL_DisableLogging) then
    exit;

  tmpFile := openSRLLogFile();

  if (tmpFile >= 0) then
    try
      writeFileString(tmpFile, '['+msToTime(getTimeRunning(), TIME_BARE)+']: '+s+#13+#10);
    except
      SRL_Warn('AddToSRLLog', 'Could not write to logfile: '+exceptionToString(exceptionType, exceptionParam), WARN_ALLVERSIONS);
    finally
      closeFile(tmpFile);
    end;
end;
